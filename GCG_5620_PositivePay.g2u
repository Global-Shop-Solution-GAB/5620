Program.Sub.ScreenSU.Start
Gui.Form..create
Gui.Form..caption("Positive Pay - Cadence Bank")
Gui.Form..size(13470,10710)
Gui.Form..position(0,0)
Gui.Form..event(UnLoad,Form_UnLoad)
Gui.Form..forecolor(0)
Gui.Form..BackColor(-2147483633)
Gui.Form..mousepointer(0)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form.dpDate.create(datepicker)
Gui.Form.dpDate.size(1935,285)
Gui.Form.dpDate.position(75,420)
Gui.Form.dpDate.defaultvalue("")
Gui.Form.cmdOK.create(button)
Gui.Form.cmdOK.caption("OK")
Gui.Form.cmdOK.size(855,375)
Gui.Form.cmdOK.position(60,1455)
Gui.Form.cmdOK.event(Click,cmdOK_Click)
Gui.Form.cmdOK.defaultvalue("")
Gui.Form.lbl1.Create(Label,"Select Date",True,1035,255,0,60,195,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtExportFolder.Create(TextBox,"",True,5580,300,0,75,1065,True,0,"Arial",8,-2147483643,1)
Gui.Form.lbl2.Create(Label,"Export Folder",True,1215,255,0,60,855,True,0,"Arial",8,-2147483633,0)
Gui.Form.cmdBrowseExportFolder.Create(Button)
Gui.Form.cmdBrowseExportFolder.Size(570,375)
Gui.Form.cmdBrowseExportFolder.Position(5745,1020)
Gui.Form.cmdBrowseExportFolder.Caption("^")
Gui.Form.cmdBrowseExportFolder.Event(Click,cmdBrowseExportFolder_Click)
Gui.Form.lbl3.Create(Label,"Courrier Code",False,1155,255,0,2070,195,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtCourcode.Create(TextBox,"USPS",False,1005,300,0,2115,405,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtCourcode.MaxLength(8)
Gui.Form.txtBankAccount.Create(TextBox,"",True,1830,300,0,2190,405,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtBankAccount.DefaultValue("")
Gui.Form.txtBankAccount.MaxLength(17)
Gui.Form.lbl4.Create(Label,"Bank Account",True,1275,255,0,2190,195,True,0,"Arial",8,-2147483633,0)
Gui.Form.gsCustom.Create(GsGridControl)
Gui.Form.gsCustom.Size(13185,7815)
Gui.Form.gsCustom.Position(75,1905)
Gui.Form.cmdRun.Create(Button)
Gui.Form.cmdRun.Enabled(False)
Gui.Form.cmdRun.Size(855,375)
Gui.Form.cmdRun.Position(45,9795)
Gui.Form.cmdRun.Caption("Run")
Gui.Form.cmdRun.DefaultValue("")
Gui.Form.cmdRun.Event(Click,cmdRun_Click)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'BN, July 24, 2019
'Customer: Clock Spring
'This project will generate a csv positive pay for Cadence Bank
'Invoice note is pulled from V_AP_INVC_NOTES that will populate the Description field of the Remittance section

f.Intrinsic.Control.Try
	v.Local.sError.Declare
	v.Local.sDefaultfolder.Declare
	v.Local.bExist.Declare
	v.Local.sRet.Declare

	F.Odbc.Connection!Conx.OpenCompanyConnection
	
	'Populate Bank Account
	'f.ODBC.Connection!conx.ExecuteAndReturn("select  Top 1 BANK_ACCOUNT_ENC from V_cash_account where default_acct = '1';",v.Local.sRet)
	f.Data.DataTable.CreateFromSQL("dtBankAccount","conx","select Top 1 BANK_ACCOUNT_ENC from V_cash_account where default_acct = '1';",True)
	f.Intrinsic.Control.If(V.DataTable.dtBankAccount.RowCount,>,0)
		F.Global.Encryption.Decrypt(V.DataTable.dtBankAccount(0).BANK_ACCOUNT_ENC!Fieldval,v.Local.sRet)
		gui.Form.txtBankAccount.Text(v.Local.sRet.Trim)
	f.Intrinsic.Control.Else
		gui.Form.txtBankAccount.Text("")	
	f.Intrinsic.Control.EndIf
	F.Data.DataTable.Close("dtBankAccount")
	
	f.Intrinsic.Control.If(v.Caller.CompanyCode,=,"FLO")
		gui.Form.txtBankAccount.Text("5500239115")
	f.Intrinsic.Control.EndIf
	
	f.Intrinsic.String.Build("{0}\Custom\5620",v.Caller.GlobalDir,v.Local.sDefaultfolder)
	f.Intrinsic.File.DirExists(v.Local.sDefaultfolder,v.Local.bExist)
	f.Intrinsic.Control.If(v.Local.bExist.Not)
		f.Intrinsic.File.CreateDir(v.Local.sDefaultfolder)
	f.Intrinsic.Control.EndIf	
	
	'Default Positive Pay folder that store the csv output file.
	f.Intrinsic.String.Build("{0}\Custom\5620\{1}",v.Caller.GlobalDir,v.Caller.CompanyCode,v.Local.sDefaultfolder)
	f.Intrinsic.File.DirExists(v.Local.sDefaultfolder,v.Local.bExist)
	f.Intrinsic.Control.If(v.Local.bExist.Not)
		f.Intrinsic.File.CreateDir(v.Local.sDefaultfolder)
	f.Intrinsic.Control.EndIf
	
	gui.Form.txtExportFolder.Text(v.Local.sDefaultfolder)
	gui.Form.gsCustom.Anchor(15)
	gui.Form.cmdRun.Anchor(6)
	gui.Form..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.cmdOK_Click.Start
'Verify if there are checks for the selected date. If yes, show the grid for the user to overwrite the courrier code
f.Intrinsic.Control.Try	
	V.Local.sError.Declare
	v.Local.sSQL.Declare
	v.Local.bExist.Declare
	V.Local.sDateSelected.Declare
	v.Local.iCnt.Declare
	v.Local.sHold.Declare	
	v.Local.iInner.Declare
	
	'Check Export Folder
	f.Intrinsic.File.DirExists(v.Screen.Form!txtExportFolder.Text.Trim,v.Local.bExist)
	f.Intrinsic.Control.If(v.Local.bExist.Not)
		f.Intrinsic.UI.Msgbox("Export Folder does not exist")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf	
	
	f.Intrinsic.UI.InvokeWaitDialog("Processing...")
	gui.Form..Visible(false)
	
	'format date
	F.Intrinsic.String.Format(V.Screen.Form!dpDate.Value,"YYYY-MM-DD",V.Local.sDateSelected)	
	
	'select data for correct date and account number
	F.intrinsic.string.Build("select Ck_Date, ltrim(rtrim(Ck_no)) as CK_No,Payee, Amount, Vend_No from V_CHECKS_LOG where CK_DATE= '{0}' and Ck_Or_Void = 'C' and Ck_No not like 'A%' order by CK_NO",v.Local.sDateSelected,V.Local.sSQL)
	f.Intrinsic.Control.If(v.DataTable.PreT.Exists)
		f.Data.DataTable.Close("PreT")
	f.Intrinsic.Control.EndIf
	f.Data.DataTable.CreateFromSQL("PreT","conx",v.Local.sSQL,True)	
	f.Intrinsic.Control.If(v.DataTable.PreT.RowCount,=,0)
		f.Intrinsic.UI.Msgbox("No checks to export")	
		f.Intrinsic.UI.CloseWaitDialog
		gui.Form..Show
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf	


	
	f.Data.DataTable.AddColumn("PreT","CourrierCode","String",v.Screen.Form!txtCourcode.Text.Trim)
	f.Data.DataTable.AddColumn("PreT","Exclude","Boolean",False)
	
	f.Intrinsic.UI.CloseWaitDialog
	f.Data.DataView.Create("PreT","PreTDV")
	'Add grid view
	
	Gui.Form.gsCustom.AddGridviewFromDataview("GVMain","PreT","PreTDV")

	'General format
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","AllowSort",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","AllowFilter",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsFindAlwaysVisible",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsViewShowAutoFilterRow",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsDetailShowDetailTabs",True)
	Gui.Form.gsCustom.SetGridViewProperty("GVMain","OptionsViewShowGroupPanel",True)
	
	'Column format	
	gui.Form.gsCustom.SetColumnProperty("GVMain","CourrierCode","AllowEdit",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","CourrierCode","ReadOnly",False)
	gui.Form.gsCustom.SetColumnProperty("GVMain","CourrierCode","Visible",False)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","AllowEdit",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","ReadOnly",False)
	'Width
	gui.Form.gsCustom.SetColumnProperty("GVMain","Vend_No","Width","40")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CourrierCode","Width","65")
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","Width","50")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Date","Width","65")
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","Width","55")
	'Header
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","HeaderHAlignment","Center")
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","HeaderHAlignment","far")
	'Header Font Bold
	gui.Form.gsCustom.SetColumnProperty("GVMain","Ck_Date","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Ck_No","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Vend_No","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Exclude","HeaderFontBold",True)
	gui.Form.gsCustom.SetColumnProperty("GVMain","Payee","HeaderFontBold",True)
	'Caption
	gui.Form.gsCustom.SetColumnProperty("GVMain","Vend_No","Caption","Vendor")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CourrierCode","Caption","Courrier Code")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Date","Caption","Check Date")
	gui.Form.gsCustom.SetColumnProperty("GVMain","CK_No","Caption","Check Number")
	
	'Numeric
	gui.Form.gsCustom.SetColumnProperty("GVMain","Amount","DisplayCustomNumeric","###,###,###,##0.00")
	
	'Custom date
	Gui.Form.gsCustom.SetColumnProperty("GVMain","CK_Date","DisplayCustomDatetime","d")
	
	'Add sub table
	F.Data.Datatable.AddTable("PreT","AP")
	f.Data.DataTable.AddColumn("PreT$AP","Check_Num","String")
	f.Data.DataTable.AddColumn("PreT$AP","Invoice","String")
	f.Data.DataTable.AddColumn("PreT$AP","Batch_Num","String")
	f.Data.DataTable.AddColumn("PreT$AP","Batch_Line","String")
	f.Data.DataTable.AddColumn("PreT$AP","Date_Batch","Date")
	f.Data.DataTable.AddColumn("PreT$AP","Date_Invoice","Date")
	f.Data.DataTable.AddColumn("PreT$AP","Amt_Invoice","float")
	f.Data.DataTable.AddColumn("PreT$AP","Amt_Transaction","float")
	f.Data.DataTable.AddColumn("PreT$AP","Discount_Invoice","float")
	f.Data.DataTable.AddExpressionColumn("PreT$AP","Amt_Net","float","[Amt_Invoice] - [Discount_Invoice]")
	f.Data.DataTable.AddColumn("PreT$AP","Purchase_Order","String")
	
	'Create Child Dataview	
	F.Data.DataView.Create("PreT$AP","APdv")	
	F.Data.DataTable.AddRelation("PreT","Ck_No","PreT$AP","Check_Num","Invoice Details")
	Gui.Form.gsCustom.AddGridviewFromDataview("gvAP","PreT","APdv")	
	
	'Custom date
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Date_Batch","DisplayCustomDatetime","d")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Date_Invoice","DisplayCustomDatetime","d")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Invoice","DisplayCustomNumeric","###,###,###,##0.00")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Transaction","DisplayCustomNumeric","###,###,###,##0.00")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Discount_Invoice","DisplayCustomNumeric","###,###,###,##0.00")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Net","DisplayCustomNumeric","###,###,###,##0.00")	
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Invoice","HeaderHAlignment","far")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Transaction","HeaderHAlignment","far")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Discount_Invoice","HeaderHAlignment","far")
	Gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Net","HeaderHAlignment","far")
	'Invisible
	gui.Form.gsCustom.SetColumnProperty("gvAP","Check_Num","Visible",False)
	'Caption
	gui.Form.gsCustom.SetColumnProperty("gvAP","Batch_Num","Caption","Batch")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Batch_Line","Caption","Line")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Date_Batch","Caption","Batch Date")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Date_Invoice","Caption","Invoice Date")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Invoice","Caption","Amt Invoice")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Transaction","Caption","Amt Transaction")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Discount_Invoice","Caption","Discount Invoice")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Amt_Net","Caption","Amt Net")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Purchase_Order","Caption","Purchase Order")
	'Width
	gui.Form.gsCustom.SetColumnProperty("gvAP","Batch_Num","Width","35")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Batch_Line","Width","35")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Date_Batch","Width","75")
	gui.Form.gsCustom.SetColumnProperty("gvAP","Date_Invoice","Width","75")
	
	'Invisible
	gui.Form.gsCustom.SetColumnProperty("gvAP","Purchase_Order","Visible",False)
	
	gui.Form.gsCustom.AddSummaryItem("gvAP","Amt_Invoice","SumAmt_Invoice","Sum","","","n")	
	gui.Form.gsCustom.AddSummaryItem("gvAP","Amt_Transaction","SumAmt_Transaction","Sum","","","n")
	gui.Form.gsCustom.AddSummaryItem("gvAP","Discount_Invoice","SumDiscount_Invoice","Sum","","","n")
	gui.Form.gsCustom.AddSummaryItem("gvAP","Amt_Net","SumAmt_Net","Sum","","","n")
	
	'Populate AP table	
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.DataTable.PreT.RowCount--,1)
		f.Intrinsic.String.Format(v.DataTable.PreT(v.Local.iCnt).Ck_Date!FieldValString,"YYYY-MM-DD",v.Local.sHold)
		f.Intrinsic.String.Build("Select ltrim(rtrim(Check_Num)) as Check_Num,Invoice,Batch_Num,Batch_Line,Date_Batch,Date_Invoice,Amt_Invoice,Amt_Transaction,Discount_Invoice,Purchase_Order from v_ap_Open_Items where Check_Num = '{0}' and Date_Check = '{1}' order by batch_num,Batch_Line",v.DataTable.PreT(v.Local.iCnt).Ck_No!FieldValSTring,v.Local.sHold,v.Local.sSQL)
		f.Data.DataTable.CreateFromSQL("Temp","conx",v.Local.sSQL,True)
		f.Intrinsic.Control.For(v.Local.iInner,0,v.DataTable.Temp.RowCount--,1)
			f.Data.Datatable.AddRow("PreT$AP","Check_Num",v.DataTable.Temp(v.Local.iInner).Check_Num!FieldValString,"Invoice",v.DataTable.Temp(v.Local.iInner).Invoice!FieldValString,"Batch_Num",v.DataTable.Temp(v.Local.iInner).Batch_Num!FieldValString,"Batch_Line",v.DataTable.Temp(v.Local.iInner).Batch_Line!FieldValString,"Date_Batch",v.DataTable.Temp(v.Local.iInner).Date_Batch!FieldValString,"Date_Invoice",v.DataTable.Temp(v.Local.iInner).Date_Invoice!FieldValString,"Amt_Invoice",v.DataTable.Temp(v.Local.iInner).Amt_Invoice!FieldValString,"Amt_Transaction",v.DataTable.Temp(v.Local.iInner).Amt_Transaction!FieldValString,"Discount_Invoice",v.DataTable.Temp(v.Local.iInner).Discount_Invoice!FieldValString,"Purchase_Order",v.DataTable.Temp(v.Local.iInner).Purchase_Order!FieldValString)
		f.Intrinsic.Control.Next(v.Local.iInner)		
		f.Data.DataTable.Close("Temp")
	f.Intrinsic.Control.Next(v.Local.iCnt)
	
	gui.Form.gsCustom.MainView("GVMain")
	gui.Form..Show
	'Deactivate Controls
	gui.Form.dpDate.Enabled(False)
	gui.Form.txtCourcode.Enabled(False)
	gui.Form.txtBankAccount.Enabled(False)
	gui.Form.txtExportFolder.Enabled(False)
	gui.Form.cmdBrowseExportFolder.Enabled(False)
	gui.Form.cmdOK.Enabled(False)
	'Activate run button
	gui.Form.cmdRun.Enabled(True)

	f.Intrinsic.Control.ExitSub
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.cmdOK_Click.End

Program.Sub.cmdRun_Click.Start
f.Intrinsic.Control.Try	
	'string
	V.Local.sError.Declare
	v.Local.sSQL.Declare	
	V.local.sExport.declare
	v.Local.sFile.Declare
	v.Local.sMsg.Declare
	V.Local.sDateSelected.Declare
	v.Local.sDateSubmitted.Declare
	v.Local.sInvoiceDate.Declare
	V.Local.sAmountCheck.Declare
	v.Local.sAmountInvoice.Declare
	v.Local.sAmtNet.Declare	
	v.Local.sCheckNum.Declare
	v.Local.sZip.Declare
	v.Local.sCourrierCode.Declare
	v.Local.sInvoiceNote.Declare
	'long
	v.Local.iCnt.Declare
	v.Local.iInvoice.Declare
	v.Local.iLineNum.Declare
	'float
	v.Local.fTotal.Declare	
	v.Local.sHold.Declare
	v.Local.sDateSubmittedWithDash.Declare
	
	v.Local.iMonth.Declare
	v.Local.iDay.Declare
	v.Local.iYear.Declare
	v.Local.sDateInvoiceWithDash.Declare
	v.Local.sPayeeName.Declare
	v.Local.sPayeeAddress1.Declare
	v.Local.sCity.Declare
	v.Local.iTotalLine.Declare
	v.Local.fAmtNet.Declare
	
	
	'Generate Dictionary (CK_No, courrierCode)
	f.Data.Dictionary.CreateFromDataTable("CourrierCodeDict","PreT","CK_No","CourrierCode")
	f.Data.Dictionary.SetDefaultReturn("CourrierCodeDict","")
	'Generate Dictionary (Invoice,Text)
	f.Data.Dictionary.CreateFromSQL("InvNoteDict","conx","Select Distinct Invoice,Text from V_AP_INVC_NOTES")
	f.Data.Dictionary.SetDefaultReturn("InvNoteDict","")
	'Generate Dictionary (Invoice,Text)
	f.Data.Dictionary.CreateFromDataTable("ExcludeDict","PreT","CK_No","Exclude")
	f.Data.Dictionary.SetDefaultReturn("ExcludeDict",False)
	
	f.Intrinsic.UI.InvokeWaitDialog("Processing...")
	gui.Form..Visible(false)
	
	'format date
	F.Intrinsic.String.Format(V.Screen.Form!dpDate.Value,"YYYY-MM-DD",V.Local.sDateSelected)	
	F.Intrinsic.String.Format(v.Ambient.Date,"MMDDYYYY",V.Local.sDateSubmitted)	
	f.Intrinsic.String.Mid(v.Local.sDateSubmitted,3,2,v.Local.sHold)
	v.Local.iMonth.Set(v.Local.sDateSubmitted.left2)
	v.Local.iDay.Set(v.Local.sHold)
	v.Local.iYear.Set(v.Local.sDateSubmitted.Right4)
	f.Intrinsic.String.Build("{0}/{1}/{2}",v.Local.iMonth,v.Local.iDay,v.Local.iYear,v.Local.sDateSubmittedWithDash)
	f.Intrinsic.String.Build("{0}\{1}_Select_{2}_Submit_{3}_{4}.csv",v.Screen.Form!txtExportFolder.Text.Trim,v.Caller.CompanyCode,v.Local.sDateSelected,v.Ambient.Date.FormatYYYYMMDD,v.Ambient.Now.FormatHhNnSs,v.Local.sFile)
	
	'1/File Header record
	f.Intrinsic.String.Build("FILHDR,PWS,Clock Spring,{0},1000,,",V.Local.sDateSubmittedWithDash,V.Local.sExport)
	f.Intrinsic.Math.Add(v.Local.iLineNum,1,v.Local.iLineNum)
	f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
	
	'select data for correct date and account number
	F.intrinsic.string.Build("select Ck_Date, Ck_No,Ck_Or_Void, Acct_No,v.Name_Vendor_pay as Payee, Amount, Vend_No,ADDRESS1_PAY as Address1,ADDRESS2_PAY as Address2,CITY_PAY as City,STATE_PAY as State,CODE_ZIP_PAY as Code_Zip,COUNTRY_PAY as Country from V_CHECKS_LOG c left join v_Vendor_Pay_To v on c.Vend_no = v.Vendor where CK_DATE = '{0}' and Ck_Or_Void = 'C' and Ck_No not like 'A%' order by CK_NO",v.Local.sDateSelected,V.Local.sSQL)
	
	f.Intrinsic.Control.If(v.DataTable.T.Exists)
		f.Data.DataTable.Close("T")
	f.Intrinsic.Control.EndIf
	f.Data.DataTable.CreateFromSQL("T","conx",v.Local.sSQL,True)
	
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.DataTable.T.RowCount--,1)
	
		'Only process the non-exclude checks
		f.Intrinsic.Control.If(v.Dictionary.ExcludeDict![v.DataTable.T(v.Local.iCnt).Ck_No!FieldValString],=,False)
			f.Intrinsic.String.Build("Select check_num,date_check,check_desc,vendor,invoice,batch_code,batch_num,batch_line,Date_batch,Date_invoice,amt_invoice,amt_transaction,discount_invoice,amt_invoice - discount_invoice as Amt_Net,purchase_order,receiver from v_AP_Open_items where vendor = '{0}' and batch_code = '11'and check_num = '{1}'",v.DataTable.T(v.Local.iCnt).Vend_No!FieldValTrim,v.DataTable.T(v.Local.iCnt).Ck_No!FieldValString,v.Local.sSQL)
			f.Intrinsic.Control.If(v.DataTable.Inv.Exists)
				f.Data.DataTable.Close("Inv")
			f.Intrinsic.Control.EndIf
			f.Data.DataTable.CreateFromSQL("Inv","conx",v.Local.sSQL,True)
			
			'If there is no invoice in AP Open Items
			f.Intrinsic.Control.If(v.DataTable.Inv.RowCount,=,0)
			
				'2/Payment header Record
				f.Intrinsic.String.Format(v.DataTable.T(v.Local.iCnt).Amount!FieldValFloat,"#.00",v.Local.sAmountCheck)		
				f.Intrinsic.String.LPad(v.DataTable.T(v.Local.iCnt).Ck_No!FieldValString,"0",9,v.Local.sCheckNum)
				'Courrier Code
				v.Local.sCourrierCode.Set(v.Dictionary.CourrierCodeDict![v.DataTable.T(v.Local.iCnt).CK_No!FieldValString])
				f.Intrinsic.String.Build("{0}{1}PMTHDR,{2},APC6US,{3},{4},{5},{6}",v.Local.sExport,v.Ambient.NewLine,v.Local.sCourrierCode.Left8,v.Local.sDateSubmittedWithDash,v.Local.sAmountCheck,v.Screen.Form!txtBankAccount.Text.Trim,v.Local.sCheckNum,v.Local.sExport)	
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'3/Payee name Record
				f.Intrinsic.String.Replace(v.DataTable.T(v.Local.iCnt).Payee!FieldValTrim,"'"," ",v.Local.sPayeeName)
				f.Intrinsic.String.Build("{0}{1}PAYENM,{2},,{3},,,",v.Local.sExport,v.Ambient.NewLine,v.Local.sPayeeName,v.DataTable.T(v.Local.iCnt).Vend_No!FieldValTrim,v.Local.sExport)	
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'4/Payee Address Record
				f.Intrinsic.String.Replace(v.DataTable.T(v.Local.iCnt).Address1!FieldValTrim,"'"," ",v.Local.sPayeeAddress1)
				f.Intrinsic.String.Build("{0}{1}PYEADD,{2},,,,,",v.Local.sExport,v.Ambient.NewLine,v.Local.sPayeeAddress1,v.Local.sExport)	
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'5/Additional Payee Address Record 
				f.Intrinsic.String.Build("{0}{1}ADDPYE,,,,,,",v.Local.sExport,v.Ambient.NewLine,v.Local.sExport)
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'6/Payee Postal Record
				f.Intrinsic.String.Left(v.DataTable.T(v.Local.iCnt).Code_Zip!FieldValTrim,10,v.Local.sZip)
				f.Intrinsic.String.Replace(v.DataTable.T(v.Local.iCnt).City!FieldValTrim,"'","",v.Local.sCity)
				f.Intrinsic.String.Build("{0}{1}PYEPOS,{2},{3},{4},{5},,",v.Local.sExport,v.Ambient.NewLine,v.Local.sCity,v.DataTable.T(v.Local.iCnt).State!FieldValTrim,v.Local.sZip,v.DataTable.T(v.Local.iCnt).Country!FieldValTrim,v.Local.sExport)
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'Add line number
				f.Intrinsic.Math.Add(v.Local.iLineNum,5,v.Local.iLineNum)
				
				'6/Remittance Detail
				f.Intrinsic.String.Build("{0}{1}RMTDTL,{2},{3},{4},{5},{5},0",v.Local.sExport,v.Ambient.NewLine,"Refund","","",v.Local.sAmountCheck,v.Local.sExport)
				f.Intrinsic.Math.Add(v.Local.iLineNum,1,v.Local.iLineNum)
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
			'There are invoices in AP Open Items
			f.Intrinsic.Control.Else
			
				f.Data.DataTable.Compute("Inv","Sum([amt_net])","",v.Local.fTotal)
				'Checking mismatch of check amount and total of invoice amount
				'f.Intrinsic.Control.If(v.Local.fTotal,<>,v.DataTable.T(v.Local.iCnt).Amount!FieldValFloat)
					'f.Intrinsic.Control.For(v.Local.iInvoice,0,v.DataTable.Inv.RowCount--,1)	
						'f.Intrinsic.String.Build("{0}{1}Invoice: {2} Amount: ${3}",v.Local.sMsg,v.Ambient.NewLine,v.DataTable.Inv(v.Local.iInvoice).Invoice!FieldValTrim,v.DataTable.Inv(v.Local.iInvoice).Amt_Invoice!FieldValFloat,v.Local.sMsg)
					'f.Intrinsic.Control.Next(v.Local.iInvoice)	
					'f.Intrinsic.String.Build("Check number {0} with Amount ${1} is not equal to Total of Invoice Amount{2}",v.DataTable.T(v.Local.iCnt).CK_No!FieldValString,v.DataTable.T(v.Local.iCnt).Amount!FieldValFloat,v.Local.sMsg,v.Local.sMsg)
					'f.Intrinsic.UI.Msgbox(v.Local.sMsg,"Error")
					'f.Intrinsic.Control.CallSub("Unload")	
				'f.Intrinsic.Control.EndIf	
			
				'2/Payment header Record
				f.Intrinsic.String.Format(v.DataTable.T(v.Local.iCnt).Amount!FieldValFloat,"#.00",v.Local.sAmountCheck)		
				f.Intrinsic.String.LPad(v.DataTable.T(v.Local.iCnt).Ck_No!FieldValString,"0",9,v.Local.sCheckNum)
				'Courrier Code
				v.Local.sCourrierCode.Set(v.Dictionary.CourrierCodeDict![v.DataTable.T(v.Local.iCnt).CK_No!FieldValString])
				f.Intrinsic.String.Build("{0}{1}PMTHDR,{2},APC6US,{3},{4},{5},{6}",v.Local.sExport,v.Ambient.NewLine,v.Local.sCourrierCode.Left8,v.Local.sDateSubmittedWithDash,v.Local.sAmountCheck,v.Screen.Form!txtBankAccount.Text.Trim,v.Local.sCheckNum,v.Local.sExport)	
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'3/Payee name Record
				f.Intrinsic.String.Replace(v.DataTable.T(v.Local.iCnt).Payee!FieldValTrim,"'"," ",v.Local.sPayeeName)
				f.Intrinsic.String.Build("{0}{1}PAYENM,{2},,{3},,,",v.Local.sExport,v.Ambient.NewLine,v.Local.sPayeeName,v.DataTable.T(v.Local.iCnt).Vend_No!FieldValTrim,v.Local.sExport)	
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'4/Payee Address Record
				f.Intrinsic.String.Replace(v.DataTable.T(v.Local.iCnt).Address1!FieldValTrim,"'"," ",v.Local.sPayeeAddress1)
				f.Intrinsic.String.Build("{0}{1}PYEADD,{2},,,,,",v.Local.sExport,v.Ambient.NewLine,v.Local.sPayeeAddress1,v.Local.sExport)	
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'5/Additional Payee Address Record 
				f.Intrinsic.String.Build("{0}{1}ADDPYE,,,,,,",v.Local.sExport,v.Ambient.NewLine,v.Local.sExport)
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'6/Payee Postal Record
				f.Intrinsic.String.Left(v.DataTable.T(v.Local.iCnt).Code_Zip!FieldValTrim,10,v.Local.sZip)
				f.Intrinsic.String.Replace(v.DataTable.T(v.Local.iCnt).City!FieldValTrim,"'","",v.Local.sCity)
				f.Intrinsic.String.Build("{0}{1}PYEPOS,{2},{3},{4},{5},,",v.Local.sExport,v.Ambient.NewLine,v.Local.sCity,v.DataTable.T(v.Local.iCnt).State!FieldValTrim,v.Local.sZip,v.DataTable.T(v.Local.iCnt).Country!FieldValTrim,v.Local.sExport)
				f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
				
				'Add line number
				f.Intrinsic.Math.Add(v.Local.iLineNum,5,v.Local.iLineNum)
				
				'6/Remittance Detail
				f.Intrinsic.Control.For(v.Local.iInvoice,0,v.DataTable.Inv.RowCount--,1)	
					'Format Date Invoice
					f.Intrinsic.String.Format(v.DataTable.Inv(v.Local.iInvoice).Date_Invoice!FieldVal,"MMDDYYYY",v.Local.sInvoiceDate)
					f.Intrinsic.String.Mid(v.Local.sInvoiceDate,3,2,v.Local.sHold)
					v.Local.iMonth.Set(v.Local.sInvoiceDate.left2)
					v.Local.iDay.Set(v.Local.sHold)
					v.Local.iYear.Set(v.Local.sInvoiceDate.Right4)
					f.Intrinsic.String.Build("{0}/{1}/{2}",v.Local.iMonth,v.Local.iDay,v.Local.iYear,v.Local.sDateInvoiceWithDash)				
				
					f.Intrinsic.String.Format(v.DataTable.Inv(v.Local.iInvoice).Amt_Transaction!FieldValFloat,"#.00",v.Local.sAmountInvoice)
					'AmtNet
					v.Local.fAmtNet.Set(v.DataTable.Inv(v.Local.iInvoice).Amt_Net!FieldValFloat)
					f.Intrinsic.Control.If(v.Local.fAmtNet,<,0)
						f.Intrinsic.Math.Mult(v.Local.fAmtNet,-1,v.Local.fAmtNet)
					f.Intrinsic.Control.EndIf
					f.Intrinsic.String.Format(v.Local.fAmtNet,"#.00",v.Local.sAmtNet)
					'Invoice note
					v.Local.sInvoiceNote.Set(v.Dictionary.InvNoteDict![v.DataTable.Inv(v.Local.iInvoice).Invoice!FieldValString])
					f.Intrinsic.String.Build("{0}{1}RMTDTL,{2},{3},{4},{5},{6},0",v.Local.sExport,v.Ambient.NewLine,v.DataTable.Inv(v.Local.iInvoice).Invoice!FieldValTrim,v.Local.sInvoiceNote.Left30,v.Local.sDateInvoiceWithDash,v.Local.sAmountInvoice,v.local.sAmtNet,v.Local.sExport)
					f.Intrinsic.Math.Add(v.Local.iLineNum,1,v.Local.iLineNum)
					f.Intrinsic.Math.Add(v.Local.iTotalLine,1,v.Local.iTotalLine)
					
				f.Intrinsic.Control.Next(v.Local.iInvoice)		
			
			f.Intrinsic.Control.EndIf
			'======End if there is/not invoice in AP open Items
			
		
		f.Intrinsic.Control.EndIf	

	f.Intrinsic.Control.Next(v.Local.iCnt)	
	'7/Trailer
	f.Intrinsic.Math.Add(v.Local.iLineNum,1,v.Local.iLineNum)
	f.Intrinsic.String.Build("{0}{1}FILTRL,{2},,,,,",v.Local.sExport,v.Ambient.NewLine,v.Local.iTotalLine,v.Local.sExport)	
	
	'Write file
	f.Intrinsic.File.String2File(v.Local.sFile,v.Local.sExport)
	
	'Msg Done and unload
	f.Intrinsic.UI.CloseWaitDialog	
	f.Intrinsic.String.Build("Generated file {0}",v.Local.sFile,v.Local.sMsg)
	f.Intrinsic.UI.Msgbox(v.Local.sMsg,"Done")
	F.Intrinsic.Control.CallSub("Unload")

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry

Program.Sub.cmdRun_Click.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.CallSub("Unload")
Program.Sub.Form_UnLoad.End

Program.Sub.UnLoad.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare

	F.Intrinsic.Control.If(V.ODBC.conx.State,=,1)
		F.ODBC.Connection!conx.close
	F.Intrinsic.Control.EndIf
	
	f.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	f.Intrinsic.String.Build("Subroutine Error {0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End	
F.Intrinsic.Control.EndTry
Program.Sub.UnLoad.End

Program.Sub.cmdBrowseExportFolder_Click.Start
'Browse Export Folder
f.Intrinsic.Control.Try
	v.Local.sError.Declare
	v.Local.sPath.Declare
	
	F.Intrinsic.UI.FolderBrowser("Select Folder for Path",V.Local.sPath)
	f.Intrinsic.Control.If(v.Local.sPath.IsCancel,=,False)
		gui.Form.txtExportFolder.Text(v.Local.sPath)
	f.Intrinsic.Control.EndIf

F.Intrinsic.Control.Catch
	f.Intrinsic.String.Build("Subroutine Error {0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End	
F.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseExportFolder_Click.End

Program.Sub.Comments.Start
${$5$}$2.0.0.0$}$2
${$6$}$sstanka$}$20210721151905251$}$mP+zz5Tw3sT2/nyap+FbWm4triMPqjXDelq42xSdLjNO5E0VKDlpebS0kf9YUBx5mwqjCgFZ8Rw=
Program.Sub.Comments.End
